buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

plugins {
    id "jacoco"
    id "org.sonarqube" version "3.5.0.2730"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    namespace "org.billthefarmer.editor"

    defaultConfig {
        applicationId "org.billthefarmer.editor"
        minSdkVersion 21
        targetSdkVersion 28
        versionName "1.96"
        versionCode 196

        buildConfigField "long", "BUILT", System.currentTimeMillis() + "L"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'ContentDescription', 'OldTargetApi', 'UnusedResources',
                'NonConstantResourceId', 'ExpiredTargetSdkVersion', 'AndroidGradlePluginVersion'
        // abortOnError false
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            finalizedBy jacocoTestReport
        }
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    reports {
        xml.enabled true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = files("${buildDir}/jacoco/testDebugUnitTest.exec")
}

sonarqube {
    properties {
        property "sonar.coverage.exclusions", ""
        property "sonar.projectKey", "marty-byrde"
        property "sonar.java.coveragePlugin", "jacoco"

        property "sonar.issue.ignore.block", "e1"
        property "sonar.issue.ignore.block.e1.beginBlockRegexp", "\\s*//\\s*START-NOSCAN"
        property "sonar.issue.ignore.block.e1.endBlockRegexp", "\\s*//\\s*END-NOSCAN"

    }
}

dependencies {
    implementation 'org.commonmark:commonmark:0.22.0'


    implementation 'org.junit.jupiter:junit-jupiter:5.11.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.1'
    testImplementation 'org.mockito:mockito-core:5.14.0'
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.11.1")
    testImplementation 'junit:junit:4.12'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.1'
}


